# Terminal Colors
# https://upload.wikimedia.org/wikipedia/commons/1/15/Xterm_256color_chart.svg

pmodload 'helper'

function +vi-git_precmd {
  # Check for untracked files, since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]+='%F{yellow}A%f'
  fi
}

function prompt_saphe_precmd {
  # Replace '/home/<user>' with '~'.
  _prompt_saphe_pwd="%F{blue}${PWD/#$HOME/~}%f"

  vcs_info
}

function prompt_saphe_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz vcs_info
  autoload -Uz add-zsh-hook

  # Add hook to set up prompt parameters before each command.
  add-zsh-hook precmd prompt_saphe_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%F{green}...%f'

  zstyle ':prezto:module:editor:info:completing' format '%B%F{green}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format '%F{blue}I%f'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format '%F{yellow}X%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{yellow}N%f'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' formats "%K{22}%F{white} %b %f%k %c%u"
  zstyle ':vcs_info:*' stagedstr '%F{green}S%f'
  zstyle ':vcs_info:*' unstagedstr '%F{red}U%f'
  zstyle ':vcs_info:*' actionformats '%F{magenta}%b%f%c%u %F{yellow}(%a)%f'
  zstyle ':vcs_info:git*+set-message:*' hooks git_precmd

  # Define prompts.
  PROMPT='
%B%F{green}%n%f%b %B${_prompt_saphe_pwd}%b ${editor_info[keymap]} ${vcs_info_msg_0_}
%F{blue}â–¸%f '
  RPROMPT='%F{blue}[%F{green}%D{%H:%M:%S}%F{blue}]%f'
  SPROMPT='zsh: correct %F{magenta}%R%f to %F{green}%r%f [nyae]? '
}

prompt_saphe_setup "$@"
